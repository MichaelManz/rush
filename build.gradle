/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the 'License');
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an 'AS IS' BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
    maven { url "http://dl.bintray.com/spinnaker/gradle" }
  }
  dependencies {
    classpath 'com.netflix.spinnaker:spinnaker-gradle-project:1.12.68'
    classpath "org.springframework.boot:spring-boot-gradle-plugin:1.1.9.RELEASE"
  }
}

allprojects {
  apply plugin: "spinnaker-gradle-project"
  apply plugin: "groovy"
  apply plugin: "spring-boot"
  apply plugin: 'spinnaker-debpublish'

  group = "com.netflix.spinnaker.rush"

  configurations.all {
    exclude group: 'javax.servlet', module: 'servlet-api'
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "org.mortbay.jetty", module: "servlet-api"
  }

  repositories {
    maven { url "http://repo.spring.io/repo" }
    maven { url 'http://oss.sonatype.org/content/repositories/staging' }
  }

  dependencies {
    spinnaker.group("test")
    spinnaker.group("bootWeb")
    spinnaker.group("retrofitDefault")
    compile spinnaker.dependency("groovy")
    compile spinnaker.dependency("kork")
    compile spinnaker.dependency("korkCassandra")
    compile spinnaker.dependency("rxJava")
    compile spinnaker.dependency('amos')
  }

  plugins.withType(JavaPlugin) {
    sourceCompatibility = "1.7"
    targetCompatibility = "1.7"
  }
}

ospackage {
  packageName = project.applicationName
  version = project.version.replaceAll("-SNAPSHOT", "")
  release '3'
  into "/opt/${project.applicationName}"
  from "${project.buildDir}/install/${project.applicationName}"
}

buildDeb {
  dependsOn installApp
  requires("openjdk-7-jre")
}

ideaConfig {
  codeStyleXml = file('idea/codeStyle.xml')
}
